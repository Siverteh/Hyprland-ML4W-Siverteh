import QtQuick
import QtQuick.Controls
import qs.widgets
import qs.services
import "welcome-catalog.js" as Catalog

Item {
    id: root
    focus: true
    readonly property bool narrow: width < 820
    property bool allShortcuts: false
    property real pageOffset: 0
    property real pageOpacity: 1
    property int previousStep: 0
    Keys.onEscapePressed: NacreWelcomeApp.close()
    Keys.onReturnPressed: if (!NacreWelcomeApp.demoBusy)
        NacreWelcomeApp.advance()
    Keys.onEnterPressed: if (!NacreWelcomeApp.demoBusy)
        NacreWelcomeApp.advance()
    function page(name) {
        NacreWelcomeApp.jump(name);
        canvas.contentY = 0;
    }
    Connections {
        target: NacreWelcomeApp
        function onPageChanged() {
            canvas.contentY = 0;
            if (NacreTokens.motionEnabled) {
                root.pageOffset = NacreWelcomeApp.step >= root.previousStep ? 24 : -24;
                root.pageOpacity = 0;
                Qt.callLater(() => {
                    root.pageOffset = 0;
                    root.pageOpacity = 1;
                });
            }
            root.previousStep = NacreWelcomeApp.step;
        }
    }
    Flickable {
        id: canvas
        objectName: "welcomeCanvas"
        width: parent.width
        height: Math.max(0, footer.y - 16)
        contentWidth: width
        contentHeight: body.implicitHeight
        clip: true
        boundsBehavior: Flickable.StopAtBounds
        maximumFlickVelocity: 3400
        FastScroll {
            view: canvas
        }
        ScrollBar.vertical: NacreScrollBar {}
        Column {
            id: body
            width: canvas.width
            spacing: 14
            x: root.pageOffset
            opacity: root.pageOpacity
            Behavior on x {
                enabled: NacreTokens.motionEnabled
                NumberAnimation {
                    duration: 170
                    easing.type: Easing.OutCubic
                }
            }
            Behavior on opacity {
                enabled: NacreTokens.motionEnabled
                NumberAnimation {
                    duration: 150
                }
            }
            Column {
                visible: NacreWelcomeApp.page === "welcome"
                width: parent.width
                spacing: 24
                Item {
                    width: 1
                    height: 30
                }
                BrandLogo {
                    anchors.horizontalCenter: parent.horizontalCenter
                    width: 200
                    height: 200
                    compact: false
                    motionEnabled: NacreTokens.motionEnabled
                }
                NacreText {
                    width: parent.width
                    text: "Nacre"
                    font.pointSize: 36
                    font.weight: Font.Medium
                    horizontalAlignment: Text.AlignHCenter
                }
                NacreText {
                    width: parent.width
                    text: "Mother-of-pearl. Colors shaped by your wallpaper."
                    font.pointSize: 14
                    wrapMode: Text.Wrap
                    horizontalAlignment: Text.AlignHCenter
                }
                NacreText {
                    width: parent.width
                    text: "A short tour of your desktop"
                    font.pointSize: 11
                    color: NacreTokens.mutedInk
                    horizontalAlignment: Text.AlignHCenter
                }
            }
            Column {
                visible: NacreWelcomeApp.page === "colors"
                width: parent.width
                spacing: 12
                NacreText {
                    text: "Your colors"
                    font.pointSize: 25
                }
                NacreText {
                    width: parent.width
                    text: "Pick a wallpaper and watch your whole desktop change live."
                    wrapMode: Text.Wrap
                    color: NacreTokens.mutedInk
                }
                NacreWelcomeDemo {
                    width: parent.width
                }
                Row {
                    spacing: 8
                    ActionButton {
                        objectName: "welcomeColors"
                        text: "Open Colors"
                        compact: true
                        onClicked: NacreWelcomeApp.route("colors")
                    }
                    ActionButton {
                        text: "More wallpapers"
                        compact: true
                        onClicked: NacreWelcomeApp.route("wallpaper")
                    }
                }
            }
            Column {
                visible: NacreWelcomeApp.page === "shortcuts"
                width: parent.width
                spacing: 14
                NacreText {
                    text: "Getting around"
                    font.pointSize: 25
                }
                NacreText {
                    text: "The keys you need first, read from your current bindings."
                    font.pointSize: 11
                    color: NacreTokens.mutedInk
                }
                Flow {
                    id: keyFlow
                    width: parent.width
                    spacing: 12
                    Repeater {
                        model: (NacreWelcomeApp.data.shortcuts || []).filter(item => root.allShortcuts || ["launcher", "terminal", "put-away", "workspace", "lock", "settings"].includes(item.id))
                        NacreSurface {
                            required property var modelData
                            width: (keyFlow.width - 12) / 2
                            height: 90
                            radius: 10
                            color: NacreTokens.raised
                            Column {
                                x: 14
                                y: 12
                                width: parent.width - 28
                                spacing: 8
                                NacreText {
                                    width: parent.width
                                    text: modelData.key
                                    font.family: NacreTokens.monoFamily
                                    font.pointSize: 13
                                    color: NacreTokens.accent
                                    wrapMode: Text.Wrap
                                }
                                NacreText {
                                    width: parent.width
                                    text: modelData.title
                                    font.pointSize: 10
                                    wrapMode: Text.Wrap
                                }
                            }
                        }
                    }
                }
                ActionButton {
                    text: root.allShortcuts ? "Show essentials" : "See all shortcuts"
                    compact: true
                    onClicked: root.allShortcuts = !root.allShortcuts
                }
                NacreText {
                    width: parent.width
                    wrapMode: Text.Wrap
                    text: NacreWelcomeApp.data.shortcutError || "Super is usually the Windows key."
                    font.pointSize: 10
                    color: NacreTokens.mutedInk
                }
            }
            Column {
                width: parent.width
                spacing: 14
                visible: NacreWelcomeApp.page === "apps"
                NacreText {
                    text: "The Nacre suite"
                    font.pointSize: 23
                }
                NacreText {
                    width: parent.width
                    text: "Tools for longer tasks, with quick controls always close by in the shell."
                    color: NacreTokens.mutedInk
                    wrapMode: Text.Wrap
                }
                NacreSurface {
                    id: setupGuide
                    width: parent.width
                    visible: !!NacreWelcomeApp.optionalApp
                    implicitHeight: setupText.implicitHeight + 32
                    radius: 12
                    color: NacreTokens.raised
                    readonly property var guide: Catalog.optional[NacreWelcomeApp.optionalApp] || {}
                    Column {
                        id: setupText
                        x: 16
                        y: 16
                        width: parent.width - 32
                        spacing: 10
                        NacreText {
                            width: parent.width
                            text: setupGuide.guide.title || ""
                            font.pointSize: 16
                            wrapMode: Text.Wrap
                        }
                        NacreText {
                            width: parent.width
                            text: setupGuide.guide.detail || ""
                            wrapMode: Text.Wrap
                        }
                        NacreText {
                            width: parent.width
                            text: setupGuide.guide.setup || ""
                            wrapMode: Text.Wrap
                            color: NacreTokens.mutedInk
                        }
                        NacreText {
                            width: parent.width
                            text: setupGuide.guide.status || ""
                            wrapMode: Text.Wrap
                            color: NacreTokens.mutedInk
                            font.pointSize: 10.5
                        }
                        ActionButton {
                            text: "Back to apps"
                            onClicked: NacreWelcomeApp.optionalApp = ""
                        }
                    }
                }
                Repeater {
                    model: Catalog.apps
                    Item {
                        required property var modelData
                        width: body.width
                        height: appRow.implicitHeight
                        BrandLogo {
                            id: appLogo
                            x: 14
                            y: 20
                            width: 44
                            height: 44
                            ai: modelData.logo === "ai"
                            brain: modelData.logo === "brain"
                            settings: modelData.logo === "settings"
                            colorsApp: modelData.logo === "colors"
                            motionEnabled: NacreTokens.motionEnabled
                            opacity: ready ? (appRow.optional ? .4 : 1) : 0
                            z: 1
                        }
                        NacreWelcomeRow {
                            id: appRow
                            objectName: "welcomeAppRow_" + modelData.logo
                            width: parent.width
                            heading: modelData.title
                            leadingSpace: 76
                            icon: modelData.icon
                            showGlyph: !appLogo.ready
                            optional: !!modelData.availability && NacreWelcomeApp.data.available?.[modelData.availability] !== true
                            buttonText: optional ? "Setup guide" : "Open"
                            available: true
                            detail: (optional ? "Optional. " : "") + modelData.detail
                            onClicked: {
                                if (optional) {
                                    NacreWelcomeApp.setup(modelData.availability);
                                    Qt.callLater(() => canvas.contentY = 0);
                                } else
                                    NacreWelcomeApp.route(modelData.action);
                            }
                        }
                    }
                }
                NacreText {
                    width: parent.width
                    text: "AI and Brain are optional. Welcome uses what is already installed; it does not set up accounts or download models."
                    color: NacreTokens.mutedInk
                    wrapMode: Text.Wrap
                    font.pointSize: 10.5
                }
            }

            Column {
                visible: NacreWelcomeApp.page === "ready"
                width: parent.width
                spacing: 18
                BrandLogo {
                    anchors.horizontalCenter: parent.horizontalCenter
                    width: 120
                    height: 120
                    compact: false
                }
                NacreText {
                    width: parent.width
                    text: "You're all set."
                    font.pointSize: 28
                    horizontalAlignment: Text.AlignHCenter
                }
                NacreText {
                    width: parent.width
                    text: (NacreWallpapers.currentEntry?.name || "Your wallpaper") + " · " + (NacreWelcomeApp.appearance.palettePersonality || "natural").replace(/^./, s => s.toUpperCase()) + " · " + (NacreWelcomeApp.appearance.paletteMode || "dark").replace(/^./, s => s.toUpperCase())
                    font.pointSize: 12
                    color: NacreTokens.mutedInk
                    wrapMode: Text.Wrap
                    horizontalAlignment: Text.AlignHCenter
                }
                NacreSwitch {
                    objectName: "welcomeStartupToggle"
                    width: parent.width
                    label: "Show at login"
                    checked: NacreWelcomeApp.data.preferences?.showAtLogin !== false
                    onToggled: value => NacreWelcomeApp.setStartup(value)
                }
                Row {
                    anchors.horizontalCenter: parent.horizontalCenter
                    spacing: 10
                    ActionButton {
                        text: "Help"
                        compact: true
                        onClicked: root.page("help")
                    }
                    ActionButton {
                        text: "Maintenance"
                        compact: true
                        onClicked: NacreWelcomeApp.route("settings:maintenance")
                    }
                }
            }
            Column {
                width: parent.width
                spacing: 14
                visible: NacreWelcomeApp.page === "help"
                NacreText {
                    text: "A little guidance, when you need it"
                    font.pointSize: 23
                }
                Repeater {
                    model: Catalog.help
                    Column {
                        required property var modelData
                        width: body.width
                        spacing: 6
                        NacreText {
                            width: parent.width
                            text: modelData.title
                            font.pointSize: 13
                            wrapMode: Text.Wrap
                        }
                        NacreText {
                            width: parent.width
                            text: modelData.detail
                            color: NacreTokens.mutedInk
                            font.pointSize: 11
                            wrapMode: Text.Wrap
                        }
                        Rectangle {
                            width: parent.width
                            height: 1
                            color: NacreTokens.outline
                            opacity: .2
                        }
                    }
                }
                Flow {
                    width: parent.width
                    spacing: 8
                    ActionButton {
                        text: "Maintenance"
                        onClicked: NacreWelcomeApp.route("settings:maintenance")
                    }
                    ActionButton {
                        text: "Display setup"
                        onClicked: NacreWelcomeApp.route("settings:displays")
                    }
                }
                NacreText {
                    text: "Read more online"
                    font.pointSize: 15
                }
                Flow {
                    width: parent.width
                    spacing: 8
                    ActionButton {
                        text: "Nacre project"
                        onClicked: NacreWelcomeApp.link("project")
                    }
                    ActionButton {
                        text: "Hyprland guide"
                        onClicked: NacreWelcomeApp.link("hyprland")
                    }
                    ActionButton {
                        text: "Quickshell docs"
                        onClicked: NacreWelcomeApp.link("quickshell")
                    }
                    ActionButton {
                        text: "CachyOS docs"
                        onClicked: NacreWelcomeApp.link("cachyos")
                    }
                }
                NacreText {
                    width: parent.width
                    text: "Nacre is a desktop for Hyprland, built with Quickshell. System packages stay with your distribution; personal configuration stays on this computer."
                    color: NacreTokens.mutedInk
                    font.pointSize: 10.5
                    wrapMode: Text.Wrap
                }
            }
        }
    }
    Item {
        id: footer
        objectName: "welcomeFooter"
        width: parent.width
        height: 64
        y: parent.height - height
        Rectangle {
            width: parent.width
            height: 1
            color: NacreTokens.outline
            opacity: .3
        }
        ActionButton {
            objectName: "welcomeBack"
            anchors.verticalCenter: parent.verticalCenter
            text: NacreWelcomeApp.step === 0 && NacreWelcomeApp.page !== "help" ? "Skip tour" : "Back"
            compact: true
            onClicked: NacreWelcomeApp.back()
        }
        Row {
            anchors.centerIn: parent
            spacing: 10
            Repeater {
                model: NacreWelcomeApp.steps
                NacreSurface {
                    required property string modelData
                    required property int index
                    objectName: "welcomeStep_" + modelData
                    width: 10
                    height: 10
                    radius: 5
                    color: NacreWelcomeApp.page === modelData ? NacreTokens.accent : NacreTokens.outline
                    NacreInteraction {
                        accessibleName: "Step " + (index + 1) + ": " + modelData
                        function onClicked() {
                            NacreWelcomeApp.jump(modelData);
                        }
                    }
                }
            }
        }
        ActionButton {
            objectName: "welcomeDone"
            anchors.right: parent.right
            anchors.verticalCenter: parent.verticalCenter
            selected: true
            text: NacreWelcomeApp.page === "ready" ? "Start using Nacre" : NacreWelcomeApp.page === "help" ? "Back to finish" : "Next"
            enabled: !NacreWelcomeApp.demoBusy
            onClicked: NacreWelcomeApp.advance()
        }
    }
}
