import QtQuick
import QtQuick.Window
import Quickshell
import qs.widgets
import qs.services

FloatingWindow {
    id: root
    title: "Nacre Colors"
    visible: NacreColorsApp.visible
    minimumSize: Qt.size(760, 560)
    implicitWidth: Math.min(1260, Math.max(760, (screen?.width || 1920) * .78))
    implicitHeight: Math.min(900, Math.max(560, (screen?.height || 1200) * .82))
    color: NacreTokens.body
    updatesEnabled: NacreColorsApp.active
    function activate() {
        minimized = false;
        contentItem.Window.window?.requestActivate();
        view.forceActiveFocus();
    }
    Component.onCompleted: {
        NacreColorsApp.window = root;
        const focused = Quickshell.screens.find(s => s.name === NacreHyprland.focusedMonitor?.name);
        if (focused)
            screen = focused;
    }
    Component.onDestruction: if (NacreColorsApp.window === root)
        NacreColorsApp.window = null
    onWindowConnected: activate()
    onClosed: NacreColorsApp.close()
    NacreAppHeader {
        id: header
        title: "Nacre Colors"
        app: "colors"
        width: parent.width
        tabs: [
            {
                id: "studio",
                name: "Studio"
            },
            {
                id: "compare",
                name: "Compare"
            },
            {
                id: "accessibility",
                name: "Accessibility"
            }
        ]
        currentTab: view.page
        mode: NacreColorsApp.options.mode
        onNavigate: name => view.page = name
        onModeChangedByUser: name => NacreColorsApp.change({
                mode: name,
                autoMode: false
            })
        onMoveRequested: root.contentItem.Window.window.startSystemMove()
    }
    NacreColorsView {
        id: view
        x: Math.max(16, (parent.width - 1360) / 2 + 16)
        y: 80
        width: Math.min(parent.width, 1360) - 32
        height: parent.height - 96
    }
    Shortcut {
        sequence: "Ctrl+W"
        context: Qt.WindowShortcut
        onActivated: NacreColorsApp.close()
    }
    Shortcut {
        sequence: "Ctrl+Return"
        context: Qt.WindowShortcut
        onActivated: NacreColorsApp.action("apply")
    }
    Shortcut {
        sequence: "Ctrl+F"
        context: Qt.WindowShortcut
        onActivated: view.search()
    }
}
