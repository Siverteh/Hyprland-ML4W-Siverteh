pragma Singleton
import QtQuick
import Quickshell
import Quickshell.Io

Singleton {
    id: root
    property bool created: false
    property bool visible: false
    property var window: null
    readonly property var client: (NacreHyprland.clients || []).find(c => c.pid === Quickshell.processId && c.title === "Nacre Colors" && c.wmClass === "org.quickshell") || null
    readonly property bool active: visible && !!window && !window.minimized && (client?.workspace?.id ?? 1) > 0
    property var options: ({
            mode: "dark",
            personality: "natural",
            backgroundFromWallpaper: false,
            overrides: {},
            brightness: 0,
            saturation: 1,
            hour: 12,
            autoMode: false,
            tideAutomatic: false,
            workspaceColors: false
        })
    property var preview: ({})
    property var library: []
    property var favorites: []
    property var history: []
    property var saved: ({
            wallpapers: {}
        })
    property int generation: 0
    property bool queued: false
    property string error: ""
    property string status: ""
    property string exportDirectory: ""
    readonly property bool previewBusy: queued || previewJob.running
    readonly property bool actionBusy: actionJob.running
    readonly property string python: Quickshell.env("HOME") + "/.local/share/nacre/palette-runtime/venv/bin/python"
    readonly property string tool: Quickshell.env("HOME") + "/.local/share/nacre/shell/tools/colors.py"
    readonly property bool ready: !!preview.id && preview.request === options && !previewBusy && !error
    readonly property bool applied: !!preview.id && saved.lastApplied === preview.id && NacreWallpapers.actualCurrent === preview.image && NacrePresentation.active.colours?.surface === preview.palette?.colours?.surface
    function open(image = "") {
        const view = NacrePanelState.getForActive();
        if (view)
            NacrePanelState.closeTransient(view);
        created = true;
        visible = true;
        if (!catalogJob.running)
            catalogJob.running = true;
        if (image || !options.image)
            choose(image || NacreWallpapers.actualCurrent || NacreWallpapers.current);
        else if (!preview.id && !previewJob.running)
            changed();
        focusAttempts = 0;
        focusTimer.restart();
        if (window)
            window.activate();
    }
    function choose(image) {
        if (!image)
            return;
        const choice = Object.assign({}, saved.wallpapers?.[image] || {});
        if (choice.backgroundFromWallpaper === undefined && choice.personality)
            choice.backgroundFromWallpaper = choice.personality !== "natural";
        if (choice.personality === "source")
            choice.personality = "natural";
        options = Object.assign({
            mode: NacreWallpapers.preferences.paletteMode || "dark",
            personality: "natural",
            backgroundFromWallpaper: NacreWallpapers.appearancePreferences.paletteBackgroundFromWallpaper ?? false,
            overrides: {},
            brightness: 0,
            saturation: 1,
            hour: NacreTime.hours,
            autoMode: false,
            tideAutomatic: false,
            workspaceColors: false
        }, choice, {
            image: image
        });
        changed();
    }
    function close() {
        visible = false;
        focusTimer.stop();
        debounce.stop();
        queued = false;
    }
    function change(values) {
        const next = Object.assign({}, options, values);
        if (Object.keys(values).some(key => ["personality", "backgroundFromWallpaper", "overrides", "brightness", "saturation", "accent", "pick"].includes(key)))
            delete next.favoriteId;
        options = next;
        changed();
    }
    function setRole(role, value) {
        const overrides = Object.assign({}, options.overrides || {});
        if (value)
            overrides[role] = value;
        else
            delete overrides[role];
        change({
            overrides: overrides
        });
    }
    function useFavorite(id) {
        change({
            favoriteId: id
        });
    }
    function useHistory(item) {
        options = Object.assign({}, item.request);
        changed();
    }
    function changed() {
        generation++;
        if (previewJob.running)
            previewJob.running = false;
        queued = true;
        error = "";
        status = "";
        debounce.restart();
    }
    function startPreview() {
        if (previewJob.running || !queued || !visible || !options.image)
            return;
        queued = false;
        previewJob.sequence = generation;
        previewJob.request = JSON.parse(JSON.stringify(options));
        previewJob.output = "";
        previewJob.failure = "";
        previewJob.running = true;
    }
    function action(name, extra = {}) {
        if (actionBusy || !ready)
            return;
        actionJob.action = name;
        actionJob.request = Object.assign({
            id: preview.id
        }, extra);
        actionJob.output = "";
        actionJob.failure = "";
        error = "";
        actionJob.running = true;
    }
    function minimize() {
        if (!client || minJob.running)
            return;
        minJob.command = ["python3", Quickshell.env("HOME") + "/.config/hypr/scripts/window-hide.py", "hide", "--window", client.address, "--quiet"];
        minJob.running = true;
    }
    property int focusAttempts: 0
    Timer {
        id: focusTimer
        interval: 80
        repeat: true
        onTriggered: {
            if (!root.visible || ++root.focusAttempts > 15) {
                stop();
                return;
            }
            if (minJob.running)
                return;
            const client = root.client;
            if (client && /^0x[0-9a-f]+$/i.test(client.address)) {
                if ((client.workspace?.id ?? 1) < 0)
                    NacreHyprland.dispatch('hl.dsp.window.move({window="address:' + client.address + '",workspace="' + Math.max(1, NacreHyprland.activeWsId) + '",follow=false})');
                NacreHyprland.dispatch('hl.dsp.focus({window="address:' + client.address + '"})');
                stop();
            }
        }
    }
    Timer {
        id: debounce
        interval: 90
        onTriggered: root.startPreview()
    }
    Process {
        id: minJob
        onExited: code => {
            if (code !== 0)
                root.error = "Could not minimize Colors.";
        }
    }
    Process {
        id: previewJob
        property int sequence: 0
        property var request: ({})
        property string output: ""
        property string failure: ""
        command: ["nice", "-n", "10", root.python, root.tool, "preview"]
        stdinEnabled: true
        onStarted: write(JSON.stringify(request) + "\n")
        stdout: StdioCollector {
            onStreamFinished: previewJob.output = text
        }
        stderr: StdioCollector {
            onStreamFinished: previewJob.failure = text
        }
        onExited: code => {
            if (sequence === root.generation) {
                if (code === 0) {
                    try {
                        const data = JSON.parse(output);
                        if (!data.id || !data.palette?.colours)
                            throw new Error("Invalid preview response");
                        root.preview = data;
                        root.options = data.request;
                    } catch (error) {
                        root.error = String(error).slice(0, 240);
                    }
                } else
                    root.error = (failure || "Preview failed; previous preview is preserved.").slice(0, 240);
            }
            if (root.queued)
                debounce.restart();
        }
    }
    Process {
        id: catalogJob
        command: [root.python, root.tool, "catalog"]
        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    const d = JSON.parse(text);
                    root.library = d.entries;
                    if (!root.options.image)
                        root.choose(d.current || d.entries[0]?.path || "");
                    root.favorites = d.favorites;
                    root.history = d.history;
                } catch (error) {
                    root.error = "Could not read the wallpaper library.";
                }
            }
        }
    }
    Process {
        id: actionJob
        property string action: "apply"
        property var request: ({})
        property string output: ""
        property string failure: ""
        command: [root.python, root.tool, action]
        stdinEnabled: true
        onStarted: write(JSON.stringify(request) + "\n")
        stdout: StdioCollector {
            onStreamFinished: actionJob.output = text
        }
        stderr: StdioCollector {
            onStreamFinished: actionJob.failure = text
        }
        onExited: code => {
            if (code !== 0) {
                root.error = (failure || "Action failed; try again.").slice(0, 240);
                return;
            }
            try {
                const data = JSON.parse(output);
                root.status = data.applied ? "Applied to the desktop" : data.saved ? "Saved " + data.saved : "Exported to " + data.directory;
                if (data.directory)
                    root.exportDirectory = data.directory;
                if (data.applied)
                    NacreWallpapers.refresh();
                if (!catalogJob.running)
                    catalogJob.running = true;
            } catch (error) {
                root.error = "Could not read the action result.";
            }
        }
    }
    FileView {
        path: Quickshell.env("HOME") + "/.config/nacre/colors.json"
        watchChanges: true
        printErrors: false
        onFileChanged: reload()
        onLoaded: {
            try {
                root.saved = JSON.parse(text());
            } catch (error) {
                root.error = "Saved colors could not be read.";
            }
        }
    }
    Connections {
        target: NacreTime
        function onHoursChanged() {
            const choice = root.saved.wallpapers?.[NacreWallpapers.actualCurrent];
            if (NacreWallpapers.preferences.palettePersonality !== "tide" || !choice?.tideAutomatic)
                return;
            const patch = {
                palettePersonality: "tide"
            };
            if (choice.autoMode)
                patch.paletteMode = NacreTime.hours >= 6 && NacreTime.hours < 19 ? "light" : "dark";
            NacreWallpapers.preference(patch);
        }
    }
    IpcHandler {
        target: "colorsApp"
        function open(image: string): void {
            root.open(image);
        }
        function preview(value: string): void {
            try {
                const values = JSON.parse(value);
                if (values.image && values.image !== root.options.image)
                    root.choose(values.image);
                root.change(values);
            } catch (error) {
                root.error = "Invalid preview request.";
            }
        }
        function close(): void {
            root.close();
        }
        function state(): string {
            return JSON.stringify({
                visible: root.visible,
                active: root.active,
                busy: root.previewBusy || root.actionBusy,
                image: root.options.image || "",
                personality: root.options.personality,
                backgroundFromWallpaper: root.options.backgroundFromWallpaper === true,
                previewId: root.preview.id || "",
                ready: root.ready,
                width: root.window?.width || 0,
                height: root.window?.height || 0,
                error: root.error,
                applied: root.applied
            });
        }
    }
}
